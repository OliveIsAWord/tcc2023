include "../include/microw8-api.cwa"

const snow_speed = 25.0;
const snow_density = 0.02;

export fn upd() {
    // tree
    randomSeed(0);
    cls(0x0b);
    let i = 20;
    loop tree {
        let w: f32 = 1000.0;
        let color = if i < 160 {
            w = (i as f32 - 20.0) * (30.0 / 140.0);
            0x88
        } else if i < 200 {
            w = (i as f32 - 130.0) * (3.0 / 40.0);
            0x66
        } else {
            0xbf
        };
        let lazy wi = (w * (randomf() + 2.0)) as i32;
        hline(160 - wi, 160 + wi, i, color);
        branch_if (i := i + 1) < 240: tree;
    }

    // snowfall
    let i = (time() * snow_speed) as i32;
    let y = 0;
    loop row {
        randomSeed(y - i);
        let x = 0;
        loop column {
            if randomf() < snow_density {
                setPixel(x, y, 0xbf);
            }
            branch_if (x := x + 1) < 320: column;
        }
        branch_if (y := y + 1) < 240: row;
    }
}
